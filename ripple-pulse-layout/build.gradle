buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

plugins {
    id 'com.chattylabs.versioning' version '0.19.0'
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply from: '../buildsystem/android2.gradle'

versioning {

    tagPrefix "version/"            // Optional. Default is empty.

    keywords {
        major "[incompatible]"      // Optional. By default it never upgrades the major version.
        minor "[feature]"           // Required
        patch "[bug]", "[patch]"    // Required
    }
}

android {
    defaultConfig {
        minSdkVersion 18
        versionCode versioning.code()
        versionName versioning.name()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

/*
 * Resolves dependency versions across test and production APKs, specifically, transitive
 * dependencies. This is required since Espresso internally has a dependency on support-annotations.
 */
configurations.all {
    resolutionStrategy.force "com.android.support:support-v4:27.1.1"
    resolutionStrategy.force "com.android.support:appcompat-v7:27.1.1"
    resolutionStrategy.force "com.android.support:support-annotations:27.1.1"
}

group = 'com.kuassivi.android.view'
version = android.defaultConfig.versionName

publish {
    userOrg = 'kuassivi'
    groupId = project.group
    artifactId = project.name
    publishVersion = project.version
    desc = 'Android Ripple Pulse Layout'
    website = 'https://github.com/kuassivi/android-ripple-pulse-layout'
}